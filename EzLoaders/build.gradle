apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 3
        versionName "1.3"
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.+'
}

/* maven deploy + signing */
group = 'com.lukekorth'
version = android.defaultConfig.versionName

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.allJava
    classpath += project.files(android.plugin.getRuntimeJarList().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

artifacts {
    archives androidJavadocsJar
    archives androidSourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'ez-loaders'

            pom.project {
                name 'ez-loaders'
                packaging 'jar'
                description 'ez-loaders is an Android library that reduces the boiler plate code necessary for Loader use'
                url 'https://github.com/lkorth/ez-loaders'

                scm {
                    url 'scm:git@github.com:lkorth/ez-loaders.git'
                    connection 'scm:git@github.com:lkorth/ez-loaders.git'
                    developerConnection 'scm:git@github.com:lkorth/ez-loaders.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lkorth'
                        name 'Luke Korth'
                    }
                }
            }
        }
    }
}