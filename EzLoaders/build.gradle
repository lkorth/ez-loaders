apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 3
        versionName "1.3"
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'ez-loaders'

            pom.project {
                name 'ez-loaders'
                groupId 'com.lukekorth'
                version = android.defaultConfig.versionName
                packaging 'jar'
                description 'ez-loaders is an Android library that reduces the boiler plate code necessary for Loader use'
                url 'https://github.com/lkorth/ez-loaders'

                scm {
                    url 'scm:git@github.com:lkorth/ez-loaders.git'
                    connection 'scm:git@github.com:lkorth/ez-loaders.git'
                    developerConnection 'scm:git@github.com:lkorth/ez-loaders.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lkorth'
                        name 'Luke Korth'
                    }
                }
            }
        }
    }
}